# Maintainer: Pig Fang <g-plane@hotmail.com>
# Contributor: SandaruKasa <sandarukasa+aur@ya.ru>

_pkgname=nil
pkgname=nil-git
pkgver=1.3.0.r66.ge53c2c6
pkgrel=1
pkgdesc="NIx Language server, an incremental analysis assistent for writing in Nix."
url="https://github.com/oxalica/nil"
arch=("x86_64")
license=("MIT")
makedepends=(cargo git)
options=(!lto)
provides=(nil)
conflicts=(nil)
source=("git+https://github.com/oxalica/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//'
}

prepare() {
  cd "$srcdir/$_pkgname"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$srcdir/$_pkgname"
  cargo build --release --frozen
}

check() {
  cd "$srcdir/$_pkgname"
  cargo test --frozen
}

package() {
  cd "$srcdir/$_pkgname"
  install -Dm755 "target/release/$_pkgname" "$pkgdir/usr/bin/$_pkgname"
}
