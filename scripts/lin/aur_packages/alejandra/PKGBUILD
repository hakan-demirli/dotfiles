# Maintainer: Pig Fang <g-plane@hotmail.com>
# Contributor: SandaruKasa <sandarukasa+aur@ya.ru>

_pkgname=alejandra
pkgname=alejandra-git
pkgver=1.3.0.r66.ge53c2c6
pkgrel=1
pkgdesc="The Uncompromising Nix Code Formatter"
url="https://github.com/kamadorueda/alejandra"
arch=("x86_64")
license=("MIT")
makedepends=(cargo git)
options=(!lto)
provides=(alejandra)
conflicts=(alejandra)
source=("git+https://github.com/kamadorueda/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//'
}

prepare() {
  cd "$srcdir/$_pkgname"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$srcdir/$_pkgname"
  cargo build --release --frozen
}

check() {
  cd "$srcdir/$_pkgname"
  cargo test --frozen
}

package() {
  cd "$srcdir/$_pkgname"
  install -Dm755 "target/release/$_pkgname" "$pkgdir/usr/bin/$_pkgname"
}
